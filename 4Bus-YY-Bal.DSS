clear

! IEEE 4-bus test case   Y-Y Stepdown Balanced
! Based on script developed by Alan Dunn and Steve Sparling

new circuit.4BusYYbal basekV=12.47 phases=3 
! **** HAVE TO STIFFEN THE SOURCE UP A LITTLE; THE TEST CASE ASSUMES AN INFINITE BUS
~ mvasc3=200000 200000

!set earthmodel=carson

! **** DEFINE WIRE DATA 
new wiredata.conductor Runits=mi Rac=0.306 GMRunits=ft GMRac=0.0244  Radunits=in Diam=0.721 
new wiredata.neutral   Runits=mi Rac=0.592 GMRunits=ft GMRac=0.00814 Radunits=in Diam=0.563 

! **** DEFINE LINE GEOMETRY; REDUCE OUT THE NEUTRAL
new linegeometry.4wire nconds=4 nphases=3 reduce=yes 
~ cond=1 wire=conductor units=ft x=-4   h=28 
~ cond=2 wire=conductor units=ft x=-1.5 h=28 
~ cond=3 wire=conductor units=ft x=3    h=28 
~ cond=4 wire=neutral   units=ft x=0    h=24 

! **** 12.47 KV LINE
!new line.line1 geometry=4wire length=2000 units=ft bus1=sourcebus bus2=n2 
new line.line1 bus1=sourcebus bus2=n2 length=2000.000 phases=3 rmatrix=[0.17332 0.05907 0.05814 |0.05907 0.17675 0.05985 |0.05814 0.05985 0.17480] xmatrix=[0.40834 0.19002 0.14580 |0.19002 0.39703 0.16047 |0.14580 0.16047 0.40343] cmatrix=[0 0 0 |0 0 0 |0 0 0]

! **** 3-PHASE STEP-DOWN TRANSFORMER 12.47/4.16 KV Y-Y
new transformer.t1 xhl=6
~ wdg=1 bus=n2 conn=wye kV=12.47 kVA=6000 %r=0.5 
~ wdg=2 bus=n3 conn=wye kV=4.16  kVA=6000 %r=0.5 sub=y

! **** 4.16 KV LINE
new line.line2 bus1=n3 bus2=n4 geometry=4wire length=2500 units=ft  

! **** WYE-CONNECTED 4.16 KV LOAD
new load.load1 phases=3 bus1=n4 conn=wye kV=4.16 kW=540 pf=1  model=2
! **** HAVE TO ALLOW P, Q TO REMAIN CONSTANT TO ABOUT .79 PU -- THIS IS ASSUMED IN TEST CASE
! **** DEFAULT IN DSS IS .95, BELOW WHICH IT REVERTS TO LINEAR MODEL
~ vminpu=0.01    ! model will remain const p,q down to 0.75 pu voltage

! **** WYE-CONNECTED 4.16 KV LOAD
new load.load2 phases=3 bus1=n4 conn=wye kV=4.16 kW=540 pf=1  model=2
! **** HAVE TO ALLOW P, Q TO REMAIN CONSTANT TO ABOUT .79 PU -- THIS IS ASSUMED IN TEST CASE
! **** DEFAULT IN DSS IS .95, BELOW WHICH IT REVERTS TO LINEAR MODEL
~ vminpu=0.01    ! model will remain const p,q down to 0.75 pu voltage

New Pvsystem.newPV_172 phases=3 bus1=n4 irradiance=1 Pmpp=600 kVA=600 %cutin=0 %cutout=0 vminpu=0.01       

set voltagebases=[12.47, 4.16] 
calcvoltagebases     ! **** let DSS compute voltage bases

